
import os
folders=['/QC','/alignment','/tmps','/macs2','/bigwig','/tags','/results']
for folder in folders:
	if not os.path.exists(config['WORKDIR']+folder):
		os.makedirs(config['WORKDIR']+folder)

workdir:config['WORKDIR']

rule all:
	input:
		peak_anno='results/'+config['macs2_experiment']+'_narrowPeak.anno',
		peak_hist='results/'+config['macs2_experiment']+'_peaks_table.txt',
		peak_heat='results/'+config['macs2_experiment']+'_peaks_heat.txt',
		bw_file=expand('bigwig/{sample}.bw',sample=config['data'].values())
rule QC:
	input:
		fq_file='raw/{sample}.fastq.gz'
	output:
		qc_file='QC/{sample}_fastqc.html'
	shell:
		"""{config[qc_bin]} -o {config[qc_dir]} -c {config[qc_con]} -a {config[qc_ada]} -l {config[qc_lim]} {input.fq_file}"""
rule align:
	input:
		fq_file='raw/{sample}.fastq.gz',
		qc_file='QC/{sample}_fastqc.html'
	output:
		aln_file='alignment/{sample}.bam'
	shell:
		"""{config[decompress]} -c {input.fq_file} | {config[bwa_bin]} mem -t {config[nop_bwa]} -T 0 -L 0 {config[ref_gen]} - | {config[sam_bin]} view -Shu - | {config[sam_bin]} sort -T tmps/{wildcards.sample} -o - - | {config[sam_bin]} rmdup -s - - >{output.aln_file}"""

rule makeTag:
	input:
		bam_file='alignment/{sample}.bam'
	output:
		out_folder='tags/{sample}'
	shell:
		"""{config[homer_makeTag]} {output.out_folder} {input.bam_file}"""
rule bam2bedgraph:
	input:
		bam_file='alignment/{sample}.bam'
	output:
		bg_file='bigwig/{sample}.bg'
	shell:
		"""{config[bed_bin]} genomecov -bga -ibam {input.bam_file} | egrep 'chr[0-9XY]' | grep -v "_" | {config[bed_bin]} sort -i - >{output.bg_file}"""
rule bedgraph2bigwig:
	input:
		bg_file='bigwig/{sample}.bg'
	output:
		wig_file='bigwig/{sample}.bw'
	shell:
		"""{config[bed_2_bw]} {input.bg_file} {config[hg19_chr_size]} {output.wig_file} """
rule peakCalling:
	input:
		treat_tag='tags/'+config['data']['treat'],
		input_tag='tags/'+config['data']['input'],
		treat_file='alignment/'+config['data']['treat']+'.bam',
		input_file='alignment/'+config['data']['input']+'.bam'
	output:
		peak_file='macs2/'+config['macs2_experiment']+'_peaks.narrowPeak'
	shell:
		"""{config[macs2_bin]} callpeak -t {input.treat_file} -c {input.input_file} -n {config[macs2_experiment]} -q 0.1 -g hs --outdir macs2"""
rule peakAnnotation:
	input:
		peak_file='macs2/'+config['macs2_experiment']+'_peaks.narrowPeak'
	output:
		peak_anno='results/'+config['macs2_experiment']+'_narrowPeak.anno'
	shell:
		"""{config[homer_annotate]} {input.peak_file} hg19 >{output.peak_anno}"""
rule tss_table:
	input:
		peak_file='macs2/'+config['macs2_experiment']+'_peaks.narrowPeak',
		tag_treat='tags/'+config['data']['treat'],
		tag_input='tags/'+config['data']['input']
	output:
		peak_hist='results/'+config['macs2_experiment']+'_peaks_table.txt'
	shell:
		"""{config[homer_annotate]} {input.peak_file} hg19 -size 6000 -hist 25 -d {input.tag_treat} {input.tag_input} >{output.peak_hist}"""
rule heatmap_table:
	input:
		peak_file='macs2/'+config['macs2_experiment']+'_peaks.narrowPeak',
		tag_treat='tags/'+config['data']['treat'],
		tag_input='tags/'+config['data']['input']
	output:
		peak_heat='results/'+config['macs2_experiment']+'_peaks_heat.txt'
	shell:
		"""{config[homer_annotate]} {input.peak_file} hg19 -size 6000 -hist 25 -ghist -d {input.tag_treat} {input.tag_input} >{output.peak_heat}"""
